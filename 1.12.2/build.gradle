buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.matthewprenger.cursegradle'


archivesBaseName = 'telegram-control-1.12.2'
version = "1.0.8"

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    compile project(':core')
}

build {
    dependencies {
        shadow project(':core')
    }
}

shadowJar {
    zip64 true
    project.configurations.shadow.setTransitive(true)
    configurations = [project.configurations.shadow]
    classifier '' // Replace the default JAR
    relocate 'com.pengrad.telegrambot', 'eu.jsan.shaded.com.pengrad.telegrambot'
    relocate 'okhttp3', 'eu.jsan.shaded.okhttp3'
    relocate 'okio', 'eu.jsan.shaded.okio'
    exclude 'META-INF/**'
    dependencies {
        exclude(dependency('com.google.code.gson:gson:.*'))
    }
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}
